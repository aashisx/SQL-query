--(Retail Analytics) problems--
-- Create Database
CREATE DATABASE RetailLabDB;
GO

USE RetailLabDB;
GO

-- Customers Table
CREATE TABLE Customers (
    CustomerID INT PRIMARY KEY IDENTITY(1,1),
    FullName VARCHAR(100) NOT NULL,
    Email VARCHAR(100) UNIQUE,
    Phone VARCHAR(20),
    City VARCHAR(50) DEFAULT 'Kathmandu',
    RegistrationDate DATE DEFAULT GETDATE()
);

-- Employees Table
CREATE TABLE Employees (
    EmployeeID INT PRIMARY KEY IDENTITY(1,1),
    FullName VARCHAR(100) NOT NULL,
    Position VARCHAR(50),
    Salary DECIMAL(10,2) CHECK (Salary > 10000),
    HireDate DATE
);

-- Products Table
CREATE TABLE Products (
    ProductID INT PRIMARY KEY IDENTITY(1,1),
    ProductName VARCHAR(100) NOT NULL,
    Category VARCHAR(50),
    Price DECIMAL(10,2) CHECK (Price > 0),
    StockQuantity INT CHECK (StockQuantity >= 0)
);

-- Orders Table
CREATE TABLE Orders (
    OrderID INT PRIMARY KEY IDENTITY(1,1),
    CustomerID INT,
    EmployeeID INT,
    OrderDate DATE DEFAULT GETDATE(),
    FOREIGN KEY (CustomerID) REFERENCES Customers(CustomerID),
    FOREIGN KEY (EmployeeID) REFERENCES Employees(EmployeeID)
);

-- OrderDetails Table
CREATE TABLE OrderDetails (
    OrderDetailID INT PRIMARY KEY IDENTITY(1,1),
    OrderID INT,
    ProductID INT,
    Quantity INT CHECK (Quantity > 0),
    UnitPrice DECIMAL(10,2),
    FOREIGN KEY (OrderID) REFERENCES Orders(OrderID),
    FOREIGN KEY (ProductID) REFERENCES Products(ProductID)
);


-- Insert Customers
INSERT INTO Customers (FullName, Email, Phone, City)
VALUES 
('Aarav Sharma', 'aarav@gmail.com', '9800000001', 'Kathmandu'),
('Sita Rai', 'sita@gmail.com', '9800000002', 'Lalitpur'),
('Ram Thapa', 'ram@gmail.com', '9800000003', 'Bhaktapur'),
('Nabin Karki', 'nabin@gmail.com', '9800000004', 'Kathmandu'),
('Priya Shrestha', 'priya@gmail.com', '9800000005', 'Pokhara');

-- Insert Employees
INSERT INTO Employees (FullName, Position, Salary, HireDate)
VALUES
('Ramesh Adhikari', 'Manager', 60000, '2021-01-10'),
('Sunita Lama', 'Sales Executive', 35000, '2022-05-12'),
('Bikash Gurung', 'Sales Executive', 32000, '2023-02-15'),
('Anita KC', 'Cashier', 25000, '2024-01-01');

-- Insert Products
INSERT INTO Products (ProductName, Category, Price, StockQuantity)
VALUES
('Laptop', 'Electronics', 80000, 10),
('Mobile', 'Electronics', 30000, 25),
('Headphones', 'Electronics', 2000, 50),
('Office Chair', 'Furniture', 7000, 15),
('Desk', 'Furniture', 12000, 5),
('Notebook', 'Stationery', 100, 200);

-- Insert Orders
INSERT INTO Orders (CustomerID, EmployeeID, OrderDate)
VALUES
(1, 2, '2025-01-10'),
(2, 3, '2025-01-12'),
(3, 2, '2025-02-01'),
(1, 1, '2025-02-10');

-- Insert OrderDetails
INSERT INTO OrderDetails (OrderID, ProductID, Quantity, UnitPrice)
VALUES
(1, 1, 1, 80000),
(1, 3, 2, 2000),
(2, 2, 1, 30000),
(3, 4, 2, 7000),
(4, 6, 10, 100);

-- 1. Display all customers with their city and registration date.
SELECT FullName, City, RegistrationDate
FROM Customers;

-- 2. Display OrderID, Customer Name, Employee Name, and Order Date.
SELECT O.OrderID, C.FullName AS CustomerName, E.FullName AS EmployeeName, O.OrderDate
FROM Orders O
JOIN Customers C ON O.CustomerID = C.CustomerID
JOIN Employees E ON O.EmployeeID = E.EmployeeID;

-- 3. Display Product Name, Category, Quantity, UnitPrice, and Total Price.
SELECT P.ProductName, P.Category, OD.Quantity, OD.UnitPrice,
       (OD.Quantity * OD.UnitPrice) AS TotalPrice
FROM OrderDetails OD
JOIN Products P ON OD.ProductID = P.ProductID;

-- 4. Display customers who have never placed any orders.
SELECT C.FullName
FROM Customers C
LEFT JOIN Orders O ON C.CustomerID = O.CustomerID
WHERE O.OrderID IS NULL;

-- 5. Display products that have never been ordered.
SELECT P.ProductName
FROM Products P
LEFT JOIN OrderDetails OD ON P.ProductID = OD.ProductID
WHERE OD.ProductID IS NULL;

-- 6. Calculate total sales amount for each order.
SELECT O.OrderID, SUM(OD.Quantity * OD.UnitPrice) AS TotalSales
FROM Orders O
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY O.OrderID;

-- 7. Display total revenue generated by each employee.
SELECT E.FullName, SUM(OD.Quantity * OD.UnitPrice) AS TotalRevenue
FROM Employees E
JOIN Orders O ON E.EmployeeID = O.EmployeeID
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY E.FullName;

-- 8. Display total quantity sold for each product (descending).
SELECT P.ProductName, SUM(OD.Quantity) AS TotalQuantitySold
FROM Products P
JOIN OrderDetails OD ON P.ProductID = OD.ProductID
GROUP BY P.ProductName
ORDER BY TotalQuantitySold DESC;

-- 9. Display the average salary grouped by Position.
SELECT Position, AVG(Salary) AS AvgSalary
FROM Employees
GROUP BY Position;

-- 10. Display total sales amount generated from each city.
SELECT C.City, SUM(OD.Quantity * OD.UnitPrice) AS TotalSales
FROM Customers C
JOIN Orders O ON C.CustomerID = O.CustomerID
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY C.City;

-- 11. Employees whose total sales > 50000 (HAVING).
SELECT E.FullName, SUM(OD.Quantity * OD.UnitPrice) AS TotalSales
FROM Employees E
JOIN Orders O ON E.EmployeeID = O.EmployeeID
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY E.FullName
HAVING SUM(OD.Quantity * OD.UnitPrice) > 50000;

-- 12. Products whose total sold quantity > 10 (HAVING).
SELECT P.ProductName, SUM(OD.Quantity) AS TotalQuantity
FROM Products P
JOIN OrderDetails OD ON P.ProductID = OD.ProductID
GROUP BY P.ProductName
HAVING SUM(OD.Quantity) > 10;

-- 13. Products whose price is higher than average price.
SELECT ProductName, Price
FROM Products
WHERE Price > (SELECT AVG(Price) FROM Products);

-- 14. Customers who placed more orders than average.
SELECT CustomerID, COUNT(OrderID) AS TotalOrders
FROM Orders
GROUP BY CustomerID
HAVING COUNT(OrderID) > (
    SELECT AVG(OrderCount)
    FROM (SELECT COUNT(OrderID) AS OrderCount
          FROM Orders
          GROUP BY CustomerID) AS AvgTable
);

-- 15. Employee who generated the highest revenue.
SELECT FullName
FROM Employees
WHERE EmployeeID = (
    SELECT TOP 1 O.EmployeeID
    FROM Orders O
    JOIN OrderDetails OD ON O.OrderID = OD.OrderID
    GROUP BY O.EmployeeID
    ORDER BY SUM(OD.Quantity * OD.UnitPrice) DESC
);

-- 16. Customer names in UPPERCASE and first 3 characters.
SELECT UPPER(FullName) AS UpperName,
       LEFT(FullName, 3) AS First3Chars
FROM Customers;

-- 17. Employeesâ€™ first name only.
SELECT LEFT(FullName, CHARINDEX(' ', FullName + ' ') - 1) AS FirstName
FROM Employees;

-- 18. Customers whose name starts with 'A'.
SELECT FullName
FROM Customers
WHERE FullName LIKE 'A%';

-- 19. Orders placed in the current month.
SELECT *
FROM Orders
WHERE MONTH(OrderDate) = MONTH(GETDATE())
AND YEAR(OrderDate) = YEAR(GETDATE());

-- 20. OrderID and number of days since order placed.
SELECT OrderID, DATEDIFF(DAY, OrderDate, GETDATE()) AS DaysSinceOrder
FROM Orders;

-- 21. Employees hired in last 2 years.
SELECT *
FROM Employees
WHERE HireDate >= DATEADD(YEAR, -2, GETDATE());

-- 22. Month-wise total sales grouped by year and month.
SELECT YEAR(O.OrderDate) AS Year, MONTH(O.OrderDate) AS Month,
SUM(OD.Quantity * OD.UnitPrice) AS TotalSales
FROM Orders O
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY YEAR(O.OrderDate), MONTH(O.OrderDate)
ORDER BY Year, Month;

-- 23. Create view vw_OrderSummary.
CREATE VIEW vw_OrderSummary AS
SELECT O.OrderID, C.FullName AS CustomerName,
SUM(OD.Quantity * OD.UnitPrice) AS TotalOrderAmount,
O.OrderDate
FROM Orders O
JOIN Customers C ON O.CustomerID = C.CustomerID
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY O.OrderID, C.FullName, O.OrderDate;

-- 24. Create view vw_EmployeeSales.
CREATE VIEW vw_EmployeeSales AS
SELECT E.FullName AS EmployeeName,
SUM(OD.Quantity * OD.UnitPrice) AS TotalSales
FROM Employees E
JOIN Orders O ON E.EmployeeID = O.EmployeeID
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY E.FullName;

-- 25. Create view vw_ProductSalesReport.
CREATE VIEW vw_ProductSalesReport AS
SELECT P.ProductName, P.Category,
 SUM(OD.Quantity) AS TotalQuantitySold,
 SUM(OD.Quantity * OD.UnitPrice) AS TotalRevenue
FROM Products P
JOIN OrderDetails OD ON P.ProductID = OD.ProductID
GROUP BY P.ProductName, P.Category;

-- 26. Display all records from vw_OrderSummary sorted by TotalOrderAmount desc.
SELECT *
FROM vw_OrderSummary
ORDER BY TotalOrderAmount DESC;

-- 27. From vw_EmployeeSales, employees with sales > 100000.
SELECT *
FROM vw_EmployeeSales
WHERE TotalSales > 100000;

-- 28. From vw_ProductSalesReport, products with revenue > 20000.
SELECT *
FROM vw_ProductSalesReport
WHERE TotalRevenue > 20000;

-- 29. Report: Customer Name, Total Orders, Total Amount Spent,  Last Order Date, and Employee who handled most recent order.
SELECT C.FullName AS CustomerName,
 COUNT(DISTINCT O.OrderID) AS TotalOrders,
 SUM(OD.Quantity * OD.UnitPrice) AS TotalAmountSpent,
 MAX(O.OrderDate) AS LastOrderDate,
 (SELECT TOP 1 E.FullName
 FROM Orders O2
 JOIN Employees E ON O2.EmployeeID = E.EmployeeID
 WHERE O2.CustomerID = C.CustomerID
 ORDER BY O2.OrderDate DESC) AS LastHandledBy
FROM Customers C
JOIN Orders O ON C.CustomerID = O.CustomerID
JOIN OrderDetails OD ON O.OrderID = OD.OrderID
GROUP BY C.CustomerID, C.FullName
ORDER BY TotalAmountSpent DESC;
